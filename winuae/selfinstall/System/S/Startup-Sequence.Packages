; Startup Sequence for HstWB Self Install Packages
; ------------------------------------------------
;
; Date: 2017-04-07
; Author: Henrik Noerfjand Stengaard


C:SetPatch QUIET


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15
FailAt 21


C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET

Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE
resident >NIL: C:UnZip PURE

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER

; Timezone for unzip
SetEnv TZ MST7

; Data types required for multiview
C:AddDataTypes REFRESH QUIET

; Paths
Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities


; Clear screen
echo "*ec" 


; Fail, if user assign doesn't exist
IF NOT EXISTS S:User-Assign
  echo "Error: User assign doesn't exist!"
  SKIP fail
ENDIF


; Run User-Assign
execute S:User-Assign


; Replace startup-sequence with workbench startup-sequence
Delete >NIL: SYSTEMDIR:S/Startup-Sequence
Rename >NIL: SYSTEMDIR:S/Startup-Sequence.Workbench SYSTEMDIR:S/Startup-Sequence


; Clear screen
echo "*ec" 


; Run Install-Packages, if it exists
IF EXISTS "HSTWBINSTALLERDIR:Install-Packages"
  execute "HSTWBINSTALLERDIR:Install-Packages"
ENDIF


; Run User-Assign
execute S:User-Assign


; Clear screen
echo "*ec" 

echo "Installation cleanup"
echo "--------------------"
echo ""
echo "Package and temp files used for installation process"
echo "will now be deleted."


; Delete temp directory, if it exist
IF EXISTS "SYSTEMDIR:Temp/FMS"
  delete >NIL: "SYSTEMDIR:Temp/FMS" ALL
ENDIF


; Delete HstWB Installer directories
echo ""
echo "Deleting HstWB Installer files..."

IF EXISTS S:Remove-HstWBInstaller
  execute S:Remove-HstWBInstaller
  delete >NIL: S:Remove-HstWBInstaller
ENDIF


; Clear screen
echo "*ec" 


; Package installation complete
echo ""
echo "Package Installation Complete"
echo "-----------------------------"
echo ""
echo "HstWB Installer has completed package"
echo "installation and system is now ready for use."
echo ""
echo "Please wait 10 seconds before restarting to allow file"
echo "system to write changes to disk. Continue will reboot"
echo "the system in 10 seconds."
echo ""
ask "Press ENTER to continue"
echo ""
echo "Rebooting in 10 seconds..."
wait 10
reboot


; End
LAB end
