; Startup Sequence for HstWB Installer
; ------------------------------------
;
; Date: 2016-11-06
; Author: Henrik Noerfjand Stengaard


; Load library for unadf
LoadModule DF0:Libs/mathieeedoubbas.library


; Clear screen
echo "*ec" 


; Installer message
echo "HstWB Installer running..."


; Set stack, failat and load basic commands resident 
stack 16384
failat 21
DF0:C/AddBuffers >NIL: DF0: 200
resident DF0:C/Assign PURE
resident DF0:C/Copy PURE
resident DF0:C/Delete PURE
resident DF0:C/Execute PURE
resident DF0:C/MakeDir PURE
resident DF0:C/Wait PURE 
resident INSTALL:C/UnADF PURE
resident INSTALL:C/UnZip PURE
SetEnv TZ MST7


; Fail, if user assign doesn't exist
IF NOT EXISTS S:User-Assign
  echo "" >INSTALL:Error
  echo "Error: User assign doesn't exist!"
  SKIP end
ENDIF
execute S:User-Assign


; Install Workbench
IF EXISTS S:Install-Workbench
  execute S:Install-Workbench
ENDIF


; Install Kickstart
IF EXISTS S:Install-Kickstart
  execute S:Install-Kickstart
ENDIF


; Install Packages
IF EXISTS S:Install-Packages
  execute S:Install-Packages
ENDIF


; Delete Installing file, if it exists
IF EXISTS S:Installing
  delete >NIL: S:Installing
ENDIF


; Done
echo ""
echo "Done."
echo "Installation is now complete."
echo ""


; End
LAB end
echo "UAE will quit in 5 seconds..."
wait 5
uaequit
