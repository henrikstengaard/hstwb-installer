; Startup Sequence for HstWB Installer
; ------------------------------------
;
; Date: 2016-01-02
; Author: Henrik Noerfjand Stengaard


; Load patched scsi.device v43.45
LoadModule DEVS:scsi.device


; Clear screen
echo "*ec" 


; Installer message
echo "HstWB Installer running..."


; Set stack, failat and load basic commands resident 
stack 16384
failat 21
DF0:C/AddBuffers >NIL: DF0: 200
resident DF0:C/Assign PURE
resident DF0:C/Copy PURE
resident DF0:C/Delete PURE
resident DF0:C/Execute PURE
resident DF0:C/Rename PURE
resident DF0:C/MakeDir PURE
resident DF0:C/Protect PURE
resident DF0:C/Search PURE 
resident DF0:C/Wait PURE 
resident DF0:C/Which PURE 
resident DF0:C/Mount PURE
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 
MakeDir RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
Assign T: RAM:
SetEnv TZ MST7


; Fail, if user assign doesn't exist
IF NOT EXISTS S:User-Assign
  echo "" >INSTALL:Error
  echo "Error: User assign doesn't exist!"
  SKIP end
ENDIF
execute S:User-Assign


; Mount and assign FMS 
assign FMS: T:
Mount FF0:


; Install Workbench
IF EXISTS S:Install-Workbench
  execute S:Install-Workbench
ENDIF


; Install Kickstart
IF EXISTS S:Install-Kickstart
  execute S:Install-Kickstart
ENDIF


; Install Packages
IF EXISTS S:Install-Packages
  execute S:Install-Packages
ENDIF


; Delete Installing file, if it exists
IF EXISTS S:Installing
  delete >NIL: S:Installing
ENDIF


; Done
echo ""
echo "Installation is now complete."
echo ""


; End
LAB end
echo "UAE will quit in 10 seconds..."
wait 10
uaequit >NIL: