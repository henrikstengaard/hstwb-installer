; Startup Sequence for Packages Installation
; ------------------------------------------
;
; Date: 2018-12-23
; Author: Henrik Noerfjand Stengaard


; Set stack
stack 16384

; Set failat
FailAt 21

; Load patched scsi.device v43.45, if it exists
IF EXISTS DEVS:scsi.device
  ; Load scsi.device, if current loaded scsi.device version is less than v43.45
  Version >NIL: "scsi.device" 43 45
  IF WARN
    LoadModule DEVS:scsi.device
  ENDIF
ENDIF

; Run setpatch quiet, if setpatch exists. Use noromupdate, if version is equal or greater than v44.13
IF EXISTS C:SetPatch
  Version >NIL: C:SetPatch 44 13 FILE
  IF WARN
    C:SetPatch QUIET
  ELSE
    C:SetPatch NOROMUPDATE QUIET
  ENDIF
ENDIF


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15


C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET

; Load commands resident
Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER


; Add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 


; Timezone for unzip
SetEnv TZ MST7

; Data types required for multiview and update screenmode
C:AddDataTypes REFRESH QUIET


; Paths
Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities


; Startup clear screen
cls


; Fail, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "Error: assign hstwb installer doesn't exist!"
  SKIP fail
ENDIF


; Run Assign-HstWB-Installer
execute S:Assign-HstWB-Installer


; Replace startup-sequence with workbench startup-sequence, if it exists
IF EXISTS SYSTEMDIR:S/Startup-Sequence.Workbench
  Copy >NIL: SYSTEMDIR:S/Startup-Sequence.Workbench SYSTEMDIR:S/Startup-Sequence
  Delete >NIL: SYSTEMDIR:S/Startup-Sequence.Workbench
ENDIF


; TODO: Causes intuition attempted to reset workbench screen errors, when installing ClassicWB packages
; TODO: Find way of getting max colors for workbench for screenshots in readme
;IF EXISTS INSTALLDIR:Prefs/Self-Install
;  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/screenmode.prefs
;    Copy >NIL: SYSTEMDIR:Prefs/Env-Archive/Sys/screenmode.prefs SYSTEMDIR:Prefs/Env-Archive/Sys/screenmode.prefs.temp
;  ENDIF
;  Copy >NIL: INSTALLDIR:Prefs/Env-Archive/Sys/screenmode.prefs SYSTEMDIR:Prefs/Env-Archive/Sys
;  C:IPrefs
;  Copy >NIL: SYSTEMDIR:Prefs/Env-Archive/Sys/screenmode.prefs.temp SYSTEMDIR:Prefs/Env-Archive/Sys/screenmode.prefs
;  Delete >NIL:SYSTEMDIR:Prefs/Env-Archive/Sys/screenmode.prefs.temp
;ENDIF


; Add assigns to installdir
Assign >NIL: C: INSTALLDIR:C ADD
Assign >NIL: Devs: INSTALLDIR:Devs ADD
Assign >NIL: L: INSTALLDIR:L ADD
Assign >NIL: Libs: INSTALLDIR:Libs ADD
Assign >NIL: Prefs: INSTALLDIR:Prefs ADD


; Load commands resident 
Resident >NIL: "INSTALLDIR:C/MakePath" PURE 
Resident >NIL: "INSTALLDIR:C/Reboot" PURE 
Resident >NIL: "INSTALLDIR:C/TotalReset" PURE
Resident >NIL: "INSTALLDIR:C/UAEquit" PURE
Resident >NIL: "INSTALLDIR:C/LhA" PURE
Resident >NIL: "INSTALLDIR:C/UNLZX" PURE
Resident >NIL: "INSTALLDIR:C/UnZip" PURE


; Run install packages
cls
IF EXISTS INSTALLDIR:System/Prefs/Install-Packages
  execute INSTALLDIR:S/Install-Packages
ENDIF


; Run install user packages
cls
IF EXISTS INSTALLDIR:Prefs/Install-User-Packages
  Execute INSTALLDIR:S/Install-User-Packages
ENDIF


; Replace startup-sequence with workbench startup-sequence
IF EXISTS S:Startup-Sequence.Workbench
  Delete >NIL: S:Startup-Sequence
  Rename >NIL: S:Startup-Sequence.Workbench S:Startup-Sequence
ENDIF


; Remove assigns from installdir
Assign >NIL: C: INSTALLDIR:C REMOVE
Assign >NIL: Devs: INSTALLDIR:Devs REMOVE
Assign >NIL: L: INSTALLDIR:L REMOVE
Assign >NIL: Libs: INSTALLDIR:Libs REMOVE
Assign >NIL: Prefs: INSTALLDIR:Prefs REMOVE


; Set use to 1, if use prefs exists
set uae "0"
IF EXISTS INSTALLDIR:Prefs/UAE
  set uae "1"
ENDIF


; Package installation complete
cls
echo "*e[32m" NOLINE
echo "Package Installation Complete"
echo "*e[0m*e[1m" NOLINE
echo "-----------------------------"
echo "*e[0m" NOLINE
echo ""
echo "HstWB Installer has completed package"
echo "installation and system is now ready for use."
echo ""

; Copy hstwb installer log to install dir, if it exists
IF EXISTS SYS:hstwb-installer.log
  Copy >NIL: SYS:hstwb-installer.log INSTALLDIR:hstwb-installer.log
ENDIF

; Run install cleanup
execute INSTALLDIR:S/Install-Cleanup

echo "Please wait 10 seconds before restarting to allow file"
echo "system to write changes to disk. Continue will reboot"
echo "the system in 10 seconds."
echo ""

; Quit UAE or reboot message
IF $uae EQ 1 VAL
  echo "Continue will quit UAE in 10 seconds."
ELSE
  echo "Continue will reboot system in 10 seconds."
ENDIF

echo ""
ask "Press ENTER to continue"


; End
; ---
LAB end

echo ""

; Quit UAE, if UAE is used. Otherwise reboot
IF $uae EQ 1 VAL
  echo "UAE will quit in 10 seconds..."
  wait 10
  UAEquit >NIL:
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  Reboot >NIL:
ENDIF