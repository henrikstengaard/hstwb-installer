; Startup Sequence for Packages Installation
; ------------------------------------------
;
; Date: 2017-08-15
; Author: Henrik Noerfjand Stengaard


; Load patched scsi.device v43.45
LoadModule DEVS:scsi.device


; Run setpatch for Workbench 3.1 or Amiga OS 3.9 depending on exec.library version
C:Version >NIL: exec.library 45 20
If WARN
  C:SetPatch QUIET
Else
  C:SetPatch NOROMUPDATE QUIET
EndIf


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15
FailAt 21


C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET

Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE
Resident >NIL: C:UnZip PURE

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER


; Add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 


; Timezone for unzip
SetEnv TZ MST7

; Data types required for multiview and update screenmode
C:AddDataTypes REFRESH QUIET

; TODO: Causes intuition attempted to reset workbench screen errors, when installing ClassicWB packages
; TODO: Find way of getting max colors for workbench for screenshots in readme
;C:IPrefs

; Paths
Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities


; Startup clear screen
cls


; Fail, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "Error: assign hstwb installer doesn't exist!"
  SKIP fail
ENDIF


; Run Assign-HstWB-Installer
execute S:Assign-HstWB-Installer


; Add assigns to installdir
Assign >NIL: C: INSTALLDIR:C ADD
Assign >NIL: Devs: INSTALLDIR:Devs ADD
Assign >NIL: L: INSTALLDIR:L ADD
Assign >NIL: Libs: INSTALLDIR:Libs ADD
Assign >NIL: Prefs: INSTALLDIR:Prefs ADD


; Load commands resident 
resident INSTALLDIR:C/Reboot PURE 
resident INSTALLDIR:C/UnZip PURE 
resident INSTALLDIR:C/UAEquit PURE 
resident INSTALLDIR:C/MakePath PURE 


; Run Install-Packages
cls
execute INSTALLDIR:S/Install-Packages


; Replace startup-sequence with workbench startup-sequence
IF EXISTS S:Startup-Sequence.Workbench
  Delete >NIL: S:Startup-Sequence
  Rename >NIL: S:Startup-Sequence.Workbench S:Startup-Sequence
ENDIF


; Remove assigns from installdir
Assign >NIL: C: INSTALLDIR:C REMOVE
Assign >NIL: Devs: INSTALLDIR:Devs REMOVE
Assign >NIL: L: INSTALLDIR:L REMOVE
Assign >NIL: Libs: INSTALLDIR:Libs REMOVE
Assign >NIL: Prefs: INSTALLDIR:Prefs REMOVE


; Set use to 1, if use prefs exists
set uae "0"
IF EXISTS INSTALLDIR:Prefs/UAE
  set uae "1"
ENDIF


; Package installation complete
cls
echo "Package Installation Complete"
echo "-----------------------------"
echo ""
echo "HstWB Installer has completed package"
echo "installation and system is now ready for use."
echo ""

; Run install cleanup
execute INSTALLDIR:S/Install-Cleanup

echo "Please wait 10 seconds before restarting to allow file"
echo "system to write changes to disk. Continue will reboot"
echo "the system in 10 seconds."
echo ""

; Quit UAE or reboot message
IF $uae EQ 1 VAL
  echo "Continue will quit UAE in 10 seconds."
ELSE
  echo "Continue will reboot system in 10 seconds."
ENDIF

echo ""
ask "Press ENTER to continue"


; End
; ---
LAB end

echo ""

; Quit UAE, if UAE is used. Otherwise reboot
IF $uae EQ 1 VAL
  echo "UAE will quit in 10 seconds..."
  wait 10
  UAEquit >NIL:
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  Reboot >NIL:
ENDIF