; Detect Amiga OS Disk
; --------------------
;
; Date: 2018-12-09
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to detect amiga os 3.1.4 or 3.1 workbench disk.


; detect amiga os disk message
cls
echo "*e[32m" NOLINE
echo "Detect Amiga OS Disk"
echo "*e[0m*e[1m" NOLINE
echo "--------------------"
echo "*e[0m" NOLINE 
echo ""
echo "To determine which Amiga OS to install, HstWB Installer"
echo "will now detect Amiga OS 3.1.4 or 3.1 Workbench disk in"
echo "any floppy device."


set detectworkbench "0"

; set detect workbench to 1, if amiga os 3.1.4 detect workbench exists
IF EXISTS "INSTALLDIR:S/Amiga-OS-3.1.4/Detect-Workbench"
  set detectworkbench "1"
ENDIF

; set detect workbench to 1, if amiga os 3.1 detect workbench exists
IF EXISTS "INSTALLDIR:S/Amiga-OS-3.1/Detect-Workbench"
  set detectworkbench "1"
ENDIF

; fail, if detect amiga os workbench doesn't exist 
IF "$detectworkbench" EQ 0 VAL
  echo "*e[1mError: Detect Amiga OS 3.1.4 or 3.1 Workbench doesn't exist!*e[0m"
  quit 20
ENDIF


; insert disk
; -----------
LAB insertdisk
echo ""
echo "Please insert Amiga OS 3.1.4 or 3.1 Workbench disk in any"
echo "floppy device."
echo ""
ask "Press ENTER to continue"

; set failat 255 for diskindrive to fail silently
failat 255

; goto detect disk, if disk is present in DF0: device
DiskInDrive >NIL: DF0:
IF $RC EQ 0 VAL
  setenv amigaosdisk "DF0"
  SKIP detectdisk
ENDIF

; goto detect disk, if disk is present in DF1: device
DiskInDrive >NIL: DF1:
IF $RC EQ 0 VAL
  setenv amigaosdisk "DF1"
  SKIP detectdisk
ENDIF

; goto detect disk, if disk is present in DF2: device
DiskInDrive >NIL: DF2:
IF $RC EQ 0 VAL
  setenv amigaosdisk "DF2"
  SKIP detectdisk
ENDIF

; goto insert disk, if disk not is present in DF3: device
DiskInDrive >NIL: DF3:
IF NOT $RC EQ 0 VAL
  echo ""
  echo "No disk is inserted in any floppy device!"
  SKIP BACK insertdisk
ENDIF

; set amigaosdisk to df3: floppy device
setenv amigaosdisk "DF3"


; detect disk
; -----------
LAB detectdisk

; set failat to 21
failat 21

; run detect amiga os 3.1.4 disk
IF EXISTS "INSTALLDIR:S/Amiga-OS-3.1.4/Detect-Workbench"
  ; run install amiga os 3.1.4, if amiga os 3.1.4 workbench disk is detected
  IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1.4/Detect-Workbench"`" EQ ""
    echo ""
    echo "Detected Amiga OS 3.1.4 Workbench disk in $amigaosdisk:"
    echo "floppy device."
    echo ""
    echo "Continue will start installation of Amiga OS 3.1.4."
    echo ""
    ask "Press ENTER to continue"

    ; run install amiga os 3.1.4 disk
    Execute "INSTALLDIR:S/Amiga-OS-3.1/Install-Amiga-OS-3.1.4"

    SKIP end
  ENDIF
ENDIF

; run detect amiga os 3.1 disk
IF EXISTS "INSTALLDIR:S/Amiga-OS-3.1/Detect-Workbench"
  ; run install amiga os 3.1, if amiga os 3.1 workbench disk is detected
  IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1/Detect-Workbench"`" EQ ""
    echo ""
    echo "Detected Amiga OS 3.1 Workbench disk in $amigaosdisk:"
    echo "floppy device."
    echo ""
    echo "Continue will start installation of Amiga OS 3.1."
    echo ""
    ask "Press ENTER to continue"

    ; run install amiga os 3.1 disk
    Execute "INSTALLDIR:S/Amiga-OS-3.1/Install-Amiga-OS-3.1"

    SKIP end
  ENDIF
ENDIF

echo ""
echo "No Amiga OS Workbench disk detected in any"
echo "floppy device."

SKIP BACK insertdisk


; end
LAB end

echo ""
ask "Press ENTER to continue"
