; User Package Installation
; -------------------------
; Author: Henrik Noerfjand Stengaard
; Date: 2018-04-13
;
; An AmigaDOS script to find, prepare and install selected user packages.

; Add assigns and set environment variables for user package installation
set userpackagesdir "`CD`"
Assign USERPACKAGESDIR: "$userpackagesdir"
Assign INSTALLDIR: "$userpackagesdir"
Assign C: "INSTALLDIR:C" ADD
Assign SYSTEMDIR: SYS:
SetEnv TZ MST7

; Copy reqtools prefs to env, if it doesn't exist
IF NOT EXISTS "ENV:ReqTools.prefs"
  copy >NIL: "INSTALLDIR:Env/ReqTools.prefs" "ENV:"
ENDIF

; Run find user packages
Execute INSTALLDIR:S/Find-User-Packages T:User-Packages

; Check if user packages contains any user packages
set userpackagescount `wc -l <T:User-Packages`
IF $userpackagescount GT 0 VAL
  ; Run prepare user packages
  echo ""
  Execute INSTALLDIR:S/Prepare-User-Packages T:User-Packages T:Install-User-Packages
  Delete >NIL: T:User-Packages

  ; Run install user packages
  Execute T:Install-User-Packages
  Delete >NIL: T:Install-User-Packages
ELSE
  echo ""
  echo "*e[1mNo user packages found!*e[22m"
ENDIF

; Remove assigns for user package installation
Assign USERPACKAGESDIR: "$userpackagesdir" REMOVE
Assign C: "INSTALLDIR:C" REMOVE
Assign INSTALLDIR: "$userpackagesdir" REMOVE
Assign SYSTEMDIR: SYS: REMOVE