; Startup Sequence for HstWB Self Install
; ---------------------------------------
;
; Date: 2017-08-14
; Author: Henrik Noerfjand Stengaard


; Load patched scsi.device v43.45
LoadModule DEVS:scsi.device


; Add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 


; Welcome message
cls 
echo "HstWB Installer"
echo "---------------"
echo ""
echo "Welcome to HstWB Installer, an installer which automates"
echo "installation of Amiga OS 3.9, Workbench 3.1, Kickstart roms"
echo "and packages with additional content."
echo ""
echo "As Amiga OS 3.9, Workbench 3.1 and Kickstart roms are still"
echo "under license and sold commercially, these are not"
echo "included and must be provided during installation process."
echo ""
echo "Amiga OS 3.9 can be bought from either http://www.vesalia.de"
echo "or http://www.amigakit.com/."
echo ""
echo "Cloanto Amiga Forever Plus Edition contains both"
echo "Workbench 3.1 and Kickstart roms and can be bought from"
echo "https://www.amigaforever.com."
echo ""
ask "Press ENTER to continue"


; Workbench message
cls 
echo "Workbench 3.1"
echo "-------------"
echo ""
echo "Workbench 3.1 system files are required for the"
echo "installation process and these will be loaded from"
echo "Workbench 3.1 disk."
echo ""
echo "Workbench 3.1 disk adf file will now be autodetected and"
echo "used from WORKBENCHDIR:, if it's present."
echo ""
echo "If WORKBENCHDIR: is not present, it will fallback to"
echo "use floppy devices and autodetect first floppy device"
echo "with Workbench 3.1 disk inserted."
echo ""
ask "Press ENTER to continue"


; Set stack
stack 16384


; Set floppydevice empty
Set floppydevice ""


; Set failat to 255 for DiskInDrive to fail silently
failat 255


; Goto insert workbench disk, if WORKBENCHDIR: is not mapped
DiskInDrive >NIL: WORKBENCHDIR:
IF WARN
  ; Set failat to 21
  failat 21
  SKIP insertworkbenchdisk
ENDIF


; Set failat to 21
failat 21


; Autodetect workbench 3.1 adf message
cls 
echo "Autodetect Workbench 3.1 adf"
echo "----------------------------"
echo ""
echo "Finding Workbench 3.1 adf in WORKBENCHDIR:..."


; Add assigns from S:AssignList
AssPlus >NIL:


; Find adf files in WORKBENCHDIR:
FSearch >RAM:_adffiles "WORKBENCHDIR:" PAT="#?.adf"

; Initialize variables
set linescount `wc -l <RAM:_adffiles`
set linenumber 1
set adffile ""

; Examine adf files
LAB start
IF NOT $linenumber GT $linescount VAL 
  ; Get adf file from line
  echo "$linenumber" >RAM:_linenumber
  set lineregexp `sed "s/\(.\)*$/\1q;d/" RAM:_linenumber`
  set line `sed "$lineregexp" "RAM:_adffiles"`

  ; Set workbench disk valid to 1
  set workbenchdiskvalid "1"

  ; Examine adf list, if xadunfile return error code 0
  xadUnFile >RAM:_adflist "$line" DIMG LIST
  IF $RC EQ 0
    ; Set workbench disk valid to 0, if adf doesn't contain which file
    grep >NIL: "C/Which" <RAM:_adflist
    IF NOT $RC EQ 0 VAL
      set workbenchdiskvalid "0"
    ENDIF

    ; Set workbench disk valid to 0, if adf doesn't contain assign file
    grep >NIL: "C/Assign" <RAM:_adflist
    IF NOT $RC EQ 0 VAL
      set workbenchdiskvalid "0"
    ENDIF

    ; Set workbench disk valid to 0, if adf doesn't contain datatypes.library file
    grep >NIL: "Libs/datatypes.library" <RAM:_adflist
    IF NOT $RC EQ 0 VAL
      set workbenchdiskvalid "0"
    ENDIF

    ; Set workbench disk valid to 0, if adf doesn't contain cli file
    grep >NIL: "System/CLI" <RAM:_adflist
    IF NOT $RC EQ 0 VAL
      set workbenchdiskvalid "0"
    ENDIF

    ; Set workbench disk valid to 0, if adf doesn't contain shell.info file
    grep >NIL: "System/Shell.info" <RAM:_adflist
    IF NOT $RC EQ 0 VAL
      set workbenchdiskvalid "0"
    ENDIF
  ELSE
    set workbenchdiskvalid "0"
  ENDIF

  ; Copy adf file, if it's a valid workbench disk
  IF "$workbenchdiskvalid" EQ "1" VAL
    set adffile "$line"
    SKIP extractworkbenchadf
  ENDIF

  ; Next
  LAB next
  JWEval "$linenumber + 1" >RAM:_linenumber
  set linenumber `sed "s/^-> //" RAM:_linenumber`
  SKIP BACK start
ENDIF


; Print workbench disk floppy device fallback message
echo "Done"
echo ""
echo "Workbench 3.1 adf doesn't exist in WORKBENCHDIR:."
echo "Installation will fallback to autodetect Workbench 3.1"
echo "disk in any floppy device."
echo ""
ask "Press ENTER to continue"
SKIP insertworkbenchdisk


; Extract workbench adf
; ---------------------
LAB extractworkbenchadf

echo "Done"
echo ""
echo "Detected Workbench 3.1 adf file:"
echo "$adffile"
echo ""
echo "Workbench 3.1 system will now be extracted from adf file"
echo "to a temp directory and prepared for self install."
echo ""
ask "Press ENTER to continue"
echo ""
echo "Preparing Workbench system files..."


; Create temp workbench directory, if it doesn't exist
IF NOT EXISTS "SYS:Temp/Workbench"
  MakePath >NIL: "SYS:Temp/Workbench"
ENDIF


; Extract files from workbench 3.1 adf
xadUnFile >NIL: "$adffile" "SYS:Temp/Workbench" DIMG OVERWRITE


; Add buffers, load commands resident and setup shell
SYS:Temp/Workbench/C/AddBuffers >NIL: DF0: 200
resident SYS:Temp/Workbench/C/Assign PURE
resident SYS:Temp/Workbench/C/Copy PURE
resident SYS:Temp/Workbench/C/Delete PURE
resident SYS:Temp/Workbench/C/Execute PURE
resident SYS:Temp/Workbench/C/Rename PURE
resident SYS:Temp/Workbench/C/MakeDir PURE
resident SYS:Temp/Workbench/C/Protect PURE
resident SYS:Temp/Workbench/C/Search PURE 
resident SYS:Temp/Workbench/C/Wait PURE 
resident SYS:Temp/Workbench/C/Which PURE 
resident SYS:Temp/Workbench/C/Mount PURE
resident SYS:Temp/Workbench/C/Type PURE
resident SYS:Temp/Workbench/C/RequestChoice PURE
MakeDir RAM:Clipboards RAM:ENV RAM:ENV/Sys
Copy ENVARC:~(#?.info) ENV: ALL QUIET
SetEnv TZ MST7


; Print prepare workbench system files done message
echo "Done"
echo ""
ask "Press ENTER to continue"
SKIP selfinstallstart


; Print insert Workbench disk
LAB insertworkbenchdisk


; Autodetect workbench 3.1 disk message
cls 
echo "Autodetect Workbench 3.1 disk"
echo "-----------------------------"
echo ""
echo "Please insert Workbench 3.1 Workbench disk in any"
echo "floppy device."
echo ""
ask "Press ENTER to continue"
echo ""


; Using fail at 255 for DiskInDrive to fail silently
failat 255

; Check if disk is present in DF0:
DiskInDrive >NIL: DF0:
IF NOT WARN
  SetEnv floppydevice "DF0"
  SKIP checkworkbenchdisk
ENDIF

; Check if disk is present in DF1:
DiskInDrive >NIL: DF1:
IF NOT WARN
  SetEnv floppydevice "DF1"
  SKIP checkworkbenchdisk
ENDIF

; Check if disk is present in DF2:
DiskInDrive >NIL: DF2:
IF NOT WARN
  SetEnv floppydevice "DF2"
  SKIP checkworkbenchdisk
ENDIF

; Check if disk is present in DF3:, if not go back to insert workbench disk
DiskInDrive >NIL: DF3:
IF NOT WARN
  SetEnv floppydevice "DF3"
ELSE
  echo "No disk is inserted in any floppy device!"
  SKIP insertworkbenchdisk back
ENDIF


; Check Workbench 3.1 Workbench disk is present in DF0, if not go back to insert workbench disk
LAB checkworkbenchdisk
failat 21

IF NOT EXISTS $floppydevice:C/Which
  echo "Workbench disk is not valid!"
  SKIP insertworkbenchdisk back
ENDIF
IF NOT EXISTS $floppydevice:C/Assign
  echo "Workbench disk is not valid!"
  SKIP insertworkbenchdisk back
ENDIF
IF NOT "`$floppydevice:c/which $floppydevice:`" EQ "Workbench3.1:"
  echo "Workbench disk is not valid!"
  SKIP insertworkbenchdisk back
ENDIF 


; Detected floppy device message
echo "Detected Workbench 3.1 disk in floppy device $floppydevice:"
echo ""
ask "Press ENTER to continue"


; Set stack, failat and load basic commands resident 
stack 16384
failat 21
$floppydevice:C/AddBuffers >NIL: DF0: 200
resident $floppydevice:C/Assign PURE
resident $floppydevice:C/Copy PURE
resident $floppydevice:C/Delete PURE
resident $floppydevice:C/Execute PURE
resident $floppydevice:C/Rename PURE
resident $floppydevice:C/MakeDir PURE
resident $floppydevice:C/Protect PURE
resident $floppydevice:C/Search PURE 
resident $floppydevice:C/Wait PURE 
resident $floppydevice:C/Which PURE 
resident $floppydevice:C/Mount PURE
resident $floppydevice:C/Type PURE
resident $floppydevice:C/RequestChoice PURE
MakeDir RAM:Clipboards RAM:ENV RAM:ENV/Sys
Copy ENVARC:~(#?.info) ENV: ALL QUIET
SetEnv TZ MST7


; Self install start
; ------------------
LAB selfinstallstart


; Fail, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "Error: Assign hstwb installer doesn't exist!"
  SKIP fail
ENDIF
execute S:Assign-HstWB-Installer


; Add assigns to installdir
Assign >NIL: C: INSTALLDIR:C ADD
Assign >NIL: Devs: INSTALLDIR:Devs ADD
Assign >NIL: L: INSTALLDIR:L ADD
Assign >NIL: Libs: INSTALLDIR:Libs ADD
Assign >NIL: Prefs: INSTALLDIR:Prefs ADD


; Fail, if HSTWBINSTALLERDIR: device name doesn't exist
Assign >NIL: EXISTS HSTWBINSTALLERDIR:
IF WARN
  echo "Error: HSTWBINSTALLERDIR: device name doesn't exist!"
  SKIP fail
ENDIF


; Load commands resident 
resident INSTALLDIR:C/Reboot PURE 
resident INSTALLDIR:C/UnZip PURE 
resident INSTALLDIR:C/UAEquit PURE 
resident INSTALLDIR:C/MakePath PURE 


; Create install prefs drawer, if it doesn't exist
IF NOT EXISTS INSTALLDIR:Prefs
  makepath INSTALLDIR:Prefs
ENDIF


; Run Patch Installation
execute "INSTALLDIR:S/Patch-Installation"


; Run Prepare Self Install
execute "INSTALLDIR:S/Prepare-Self-Install"


; Run Install-Start
execute "INSTALLDIR:S/Install-Start"


; Goto self install complete, if floppy device is empty
IF "$floppydevice" EQ ""
  SKIP selfinstallcomplete
ENDIF


; Print eject disk message
cls
echo "Eject disk"
echo "----------"
echo ""
echo "A system reboot is required for installation process"
echo "to continue and floppy disk must be ejected."


; Check if disk is present in floppy device, if present go back to eject disk
LAB ejectdisk
DiskInDrive $floppydevice:
IF NOT WARN
  echo ""
  echo "Please eject disk from $floppydevice:"
  echo ""
  ask "Press ENTER to continue"
  SKIP ejectdisk back
ENDIF


; Complete
; --------
LAB selfinstallcomplete

; Clean boot self install
IF EXISTS "SYS:C/AssPlus"
  Delete >NIL: "SYS:C/AssPlus"
ENDIF
IF EXISTS "SYS:C/DiskInDrive"
  Delete >NIL: "SYS:C/DiskInDrive"
ENDIF
IF EXISTS "SYS:C/FSearch"
  Delete >NIL: "SYS:C/FSearch"
ENDIF
IF EXISTS "SYS:C/grep"
  Delete >NIL: "SYS:C/grep"
ENDIF
IF EXISTS "SYS:C/JWEval"
  Delete >NIL: "SYS:C/JWEval"
ENDIF
IF EXISTS "SYS:C/MakePath"
  Delete >NIL: "SYS:C/MakePath"
ENDIF
IF EXISTS "SYS:C/sed"
  Delete >NIL: "SYS:C/sed"
ENDIF
IF EXISTS "SYS:C/wc"
  Delete >NIL: "SYS:C/wc"
ENDIF
IF EXISTS "SYS:C/xadUnFile"
  Delete >NIL: "SYS:C/xadUnFile"
ENDIF
IF EXISTS "SYS:Libs/xad"
  Delete >NIL: "SYS:Libs/xad" ALL
ENDIF
IF EXISTS "SYS:Libs/xadmaster.library"
  Delete >NIL: "SYS:Libs/xadmaster.library"
ENDIF
IF EXISTS "SYS:S/AssignList"
  Delete >NIL: "SYS:S/AssignList"
ENDIF
IF EXISTS "SYS:S/xadmaster.key"
  Delete >NIL: "SYS:S/xadmaster.key"
ENDIF
IF EXISTS "SYS:Temp/Workbench"
  Delete >NIL: "SYS:Temp/Workbench" ALL
ENDIF


; Remove assigns from installdir
Assign >NIL: C: INSTALLDIR:C REMOVE
Assign >NIL: Devs: INSTALLDIR:Devs REMOVE
Assign >NIL: L: INSTALLDIR:L REMOVE
Assign >NIL: Libs: INSTALLDIR:Libs REMOVE
Assign >NIL: Prefs: INSTALLDIR:Prefs REMOVE


; Run Install-Complete
execute "INSTALLDIR:S/Install-Complete"

SKIP end


; Fail
LAB fail
echo ""
echo "Installation failed."
quit


; End
LAB end