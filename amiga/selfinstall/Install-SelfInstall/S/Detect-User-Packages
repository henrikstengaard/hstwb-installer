; Detect User Packages
; --------------------
;
; Date: 2019-01-04
; Author: Henrik Noerfjand Stengaard


; detect user packages
; --------------------
LAB detectuserpackages

; delete install user packages prefs, if it exists
IF EXISTS INSTALLDIR:Prefs/Install-User-Packages
  Delete >NIL: INSTALLDIR:Prefs/Install-User-Packages
ENDIF

echo "" NOLINE >"INSTALLDIR:Temp/Detect-User-Packages.log"
echo "Detect user packages" >>"INSTALLDIR:Temp/Detect-User-Packages.log"
echo "--------------------" >>"INSTALLDIR:Temp/Detect-User-Packages.log"

cls 
echo "*e[32m" NOLINE
echo "Detect user packages"
echo "*e[0m*e[1m" NOLINE
echo "--------------------"
echo "*e[0m" NOLINE
echo ""
echo "*e[1mDetecting user packages in USERPACKAGESDIR:*e[0m"

; set failat 255 for diskindrive to fail silently
failat 255

; set userpackagesdir to 1 and run disk change, if "USERPACKAGESDIR:" device exists
set userpackagesdir "0"
DiskInDrive >NIL: USERPACKAGESDIR:
IF $RC EQ 0 VAL
  set userpackagesdir "1"

  ; disk change USERPACKAGESDIR: device to detect changed directories and files
  DiskChange USERPACKAGESDIR:
ENDIF

; set failat to 21
failat 21

; goto end, if userpackagesdir is not set to 1
IF NOT "$userpackagesdir" EQ 1 VAL
  echo "USERPACKAGESDIR: device doesn't exist or is not mapped to a directory!" >>"INSTALLDIR:Temp/Detect-User-Packages.log"
  echo "USERPACKAGESDIR: device doesn't exist or is not mapped to a directory!"
ENDIF

; run detect user packages
set dummy "`Execute "INSTALLDIR:User-Packages/S/Detect-User-Packages" "INSTALLDIR:Prefs/User-Packages"`"

; show user packages, if one or more user packages are detected
set userpackagescount `wc -l <INSTALLDIR:Prefs/User-Packages`
IF "$userpackagescount" GT 0 VAL
  ; show user packages detected
  type "INSTALLDIR:Prefs/User-Packages" >>"INSTALLDIR:Temp/Detect-User-Packages.log"
  type "INSTALLDIR:Prefs/User-Packages"
ELSE
  ; show no user packages detected
  echo "No user packages detected!" >>"INSTALLDIR:Temp/Detect-User-Packages.log"
  echo "No user packages detected!"
ENDIF

echo "Done"
echo ""
echo "Continue will show 'User packages available' menu."
echo ""
ask "Press ENTER to continue"


; user packages menu
; ------------------
LAB userpackagesmenu

IF "$userpackagescount" GT 0 VAL
  Copy >NIL: "INSTALLDIR:Prefs/User-Packages" "T:_user-packages-menu"
ELSE
  echo "No user packages detected!" >"T:_user-packages-menu"
ENDIF

echo "--------------------------------------------------" >>"T:_user-packages-menu"
echo "Help" >>"T:_user-packages-menu"
echo "Show detect user packages log" >>"T:_user-packages-menu"
echo "Restart detect user packages" >>"T:_user-packages-menu"
echo "Continue with $userpackagescount user package(s) available" >>"T:_user-packages-menu"

; show user packages menu
set userpackagesoption ""
set userpackagesoption "`RequestList TITLE="User packages available" LISTFILE="T:_user-packages-menu" WIDTH=640 LINES=24`"
Delete >NIL: "T:_user-packages-menu"

; set initial menu option
IF "$userpackagescount" GT 0 VAL
  set menuoption "$userpackagescount"
ELSE
  set menuoption "1"
ENDIF

; help option
set menuoption `eval $menuoption + 2`
IF "$userpackagesoption" EQ $menuoption VAL
  Lister "INSTALLDIR:Help/User-Packages-Available.txt" >NIL:
ENDIF

; show detect user packages log option
set menuoption `eval $menuoption + 1`
IF "$userpackagesoption" EQ $menuoption VAL
  IF EXISTS "INSTALLDIR:Temp/Detect-User-Packages.log"
    Lister "INSTALLDIR:Temp/Detect-User-Packages.log" >NIL:
  ELSE
    RequestChoice "Error" "Detect User Packages log doesn't exist!" "OK" >NIL:
  ENDIF
ENDIF

; restart detect user packages option
set menuoption `eval $menuoption + 1`
IF "$userpackagesoption" EQ $menuoption VAL
  set confirm "`RequestChoice "User packages available" "Do you want to restart detect user packages?" "Yes|No"`"
  IF "$confirm" EQ 1 VAL
    SKIP BACK detectuserpackages
  ENDIF
ENDIF

; continue option
set menuoption `eval $menuoption + 1`
IF "$userpackagesoption" EQ $menuoption VAL
  set confirm "`RequestChoice "User packages available" "Do you want to continue with $userpackagescount*Nuser package(s) available?" "Yes|No"`"
  IF "$confirm" EQ 1 VAL
    ; create install user packages prefs and install user packages, if one or more user packages are detected
    IF "$userpackagescount" GT 0 VAL
      ; create install user packages prefs
      echo "" >INSTALLDIR:Prefs/Install-User-Packages

      ; run build install user packages
      Copy >NIL: INSTALLDIR:Prefs/User-Packages T:User-Packages
      Execute INSTALLDIR:User-Packages/S/Build-Install-User-Packages T:User-Packages INSTALLDIR:S/Install-User-Packages INSTALL
      Delete >NIL: T:User-Packages
    ENDIF

    ; add detect user packages to log
    type "INSTALLDIR:Temp/Detect-User-Packages.log" >>SYS:hstwb-installer.log

    SKIP end
  ENDIF
ENDIF

SKIP BACK userpackagesmenu


; end
LAB end
