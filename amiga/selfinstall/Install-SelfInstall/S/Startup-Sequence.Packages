; Startup Sequence for HstWB Self Install Packages
; ------------------------------------------------
;
; Date: 2017-08-02
; Author: Henrik Noerfjand Stengaard


; Load patched scsi.device v43.45
LoadModule DEVS:scsi.device


C:Version >NIL: exec.library 45 20
If WARN
  C:SetPatch QUIET
Else
  C:SetPatch NOROMUPDATE QUIET
EndIf


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15
FailAt 21


C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET

Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE
Resident >NIL: C:UnZip PURE

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER

; Timezone for unzip
SetEnv TZ MST7

; Data types required for multiview and update screenmode
C:AddDataTypes REFRESH QUIET

; TODO: Causes intuition attempted to reset workbench screen errors, when installing ClassicWB packages
; TODO: Find way of getting max colors for workbench for screenshots in readme
;C:IPrefs

; Paths
Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities


; Clear screen
echo "*ec" 


; Fail, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "Error: assign hstwb installer doesn't exist!"
  SKIP fail
ENDIF


; Run Assign-HstWB-Installer
execute S:Assign-HstWB-Installer


; Delete fms temp directory, if it exist
IF EXISTS "SYSTEMDIR:Temp/FMS"
  delete >NIL: "SYSTEMDIR:Temp/FMS" ALL
ENDIF


; Replace startup-sequence with workbench startup-sequence
IF EXISTS S:Startup-Sequence.Workbench
  Delete >NIL: S:Startup-Sequence
  Rename >NIL: S:Startup-Sequence.Workbench S:Startup-Sequence
ENDIF


; Clear screen
echo "*ec" 


; Run Install-Packages
execute INSTALL:S/Install-Packages


; Clear screen
echo "*ec" 

echo "Installation cleanup"
echo "--------------------"
echo ""
echo "Package and temp files used for installation process"
echo "will now be deleted."


; Delete HstWB Installer directories
echo ""
echo "Deleting HstWB Installer files..."

IF EXISTS S:Remove-HstWBInstaller
  execute S:Remove-HstWBInstaller
  delete >NIL: S:Remove-HstWBInstaller
ENDIF

echo "Done."
echo ""
ask "Press ENTER to continue"


; Clear screen
echo "*ec" 


; Package installation complete
echo "Package Installation Complete"
echo "-----------------------------"
echo ""
echo "HstWB Installer has completed package"
echo "installation and system is now ready for use."
echo ""
echo "Please wait 10 seconds before restarting to allow file"
echo "system to write changes to disk. Continue will reboot"
echo "the system in 10 seconds."
echo ""
ask "Press ENTER to continue"
echo ""
echo "Rebooting in 10 seconds..."
wait 10
reboot


; End
LAB end