; Startup Sequence for HstWB Installer
; ------------------------------------
;
; Date: 2017-01-02
; Author: Henrik Noerfjand Stengaard


; Load patched scsi.device v43.45
LoadModule DEVS:scsi.device


; Clear screen
echo "*ec" 


; Installer message
echo "HstWB Installer building self install..."


; Set stack, failat and load basic commands resident 
stack 16384
failat 21
DF0:C/AddBuffers >NIL: DF0: 200
resident DF0:C/Assign PURE
resident DF0:C/Copy PURE
resident DF0:C/Delete PURE
resident DF0:C/Execute PURE
resident DF0:C/MakeDir PURE
resident DF0:C/Protect PURE
resident DF0:C/Search PURE 
resident DF0:C/Wait PURE 
MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
SetEnv TZ MST7


; Fail, if user assign doesn't exist
IF NOT EXISTS S:User-Assign
  echo "" >INSTALL:Error
  echo "Error: User assign doesn't exist!"
  SKIP end
ENDIF
execute S:User-Assign


echo ""
echo "Copying system..."
copy >NIL: "INSTALL:System/" TO "SYSTEMDIR:" ALL
echo "Done."


; Copy HstWB Installer
echo ""
echo "Copying HstWB Installer..."
copy >NIL: "INSTALL:HstWBInstaller/" TO "HSTWBINSTALLERDIR:" ALL
echo "Done."


; Create packages directory, if it doesn't exist
IF NOT EXISTS "HSTWBINSTALLERDIR:Packages/"
  makedir >NIL: "HSTWBINSTALLERDIR:Packages"
ENDIF


; Copy Packages
echo ""
echo "Copying packages..."
copy >NIL: "PACKAGES:" TO "HSTWBINSTALLERDIR:Packages/" ALL
echo "Done."


; Delete Installing file, if it exists
IF EXISTS S:Installing
  delete >NIL: S:Installing
ENDIF


; Done
echo ""
echo "Installation is now complete."
echo ""


; End
LAB end
echo "UAE will quit in 10 seconds..."
wait 10
uaequit >NIL:
