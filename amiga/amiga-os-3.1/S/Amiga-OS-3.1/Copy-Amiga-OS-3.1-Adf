.KEY file/a,dir/a
.BRA {
.KET }

; Copy Amiga OS 3.1 Adf
; ---------------------
;
; Date: 2018-11-26
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to identify and copy valid Amiga OS 3.1 adf files.


; goto end, if file doesn't exist
IF NOT EXISTS "{file}"
  SKIP end
ENDIF

; goto end, if dir doesn't exist
IF NOT EXISTS "{dir}"
  SKIP end
ENDIF

; get md5 for file
set md5 ""
set md5 "`md5 "{file}"`"

; copy cloanto amiga forever 2016 amiga os 3.1 workbench adf, if file md5 matches
IF $md5 eq "590c42a69675d6970df350e200fe25dc"
  echo "Cloanto Amiga Forever 2016 Amiga OS 3.1 Workbench adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-workbench.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7 amiga os 3.1 workbench adf, if file md5 matches
IF $md5 eq "e7b3a83df665a85e7ec27306a152b171"
  echo "Cloanto Amiga Forever 7 Amiga OS 3.1 Workbench adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-workbench.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 storage adf, if file md5 matches
IF $md5 eq "4fa1401aeb814d3ed138f93c54a5caef"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Storage adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-storage.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 Locale adf, if file md5 matches
IF $md5 eq "b53c9ff336e168643b10c4a9cfff4276"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Locale adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-locale.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 2016 amiga os 3.1 install adf, if file md5 matches
IF $md5 eq "d6aa4537586bf3f2687f30f8d3099c99"
  echo "Cloanto Amiga Forever 2016 Amiga OS 3.1 Install adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-install.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7 amiga os 3.1 install adf, if file md5 matches
IF $md5 eq "c5be06daf40d4c3ace4eac874d9b48b1"
  echo "Cloanto Amiga Forever 7 Amiga OS 3.1 Install adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-install.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 fonts adf, if file md5 matches
IF $md5 eq "6fae8b94bde75497021a044bdbf51abc"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Fonts adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-fonts.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 extras adf, if file md5 matches
IF $md5 eq "c1c673eba985e9ab0888c5762cfa3d8f"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Extras adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffile "amiga-os-310-extras.adf"
  SKIP copyadffile
ENDIF


; generic
; -------

; goto end, if file size is not 901120 bytes
set filesize `wc -c <"{file}"`
IF NOT $filesize EQ 901120 VAL
  SKIP end
ENDIF

; goto end, if xadunfile doesn't return error code 0
xadUnFile >T:_adflist "{file}" DIMG LIST
IF NOT $RC EQ 0 VAL
  SKIP end
ENDIF

; copy amiga os 3.1 workbench adf, if it contains text "Workbench 3.1" and adf is valid
FSearch >T:_diskname "{file}" TXT="Workbench3.1"
set diskname `wc -l <T:_diskname`
IF NOT $diskname EQ 0 VAL
  set adfvalid 1

  ; set workbench adf valid 0, if adf doesn't contain which file
  grep >NIL: -i "C/Which" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set adfvalid 0
  ENDIF

  ; set workbench adf valid 0, if adf doesn't contain assign file
  grep >NIL: -i "C/Assign" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set adfvalid 0
  ENDIF

  ; set workbench adf valid 0, if adf doesn't contain datatypes.library file
  grep >NIL: -i "Libs/datatypes.library" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set adfvalid 0
  ENDIF

  ; set workbench adf valid 0, if adf doesn't contain cli file
  grep >NIL: -i "System/CLI" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set adfvalid 0
  ENDIF

  ; Set workbench adf valid 0, if adf doesn't contain shell.info file
  grep >NIL: -i "System/Shell.info" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set adfvalid 0
  ENDIF

  ; copy amiga os 3.1 workbench file, if workbench adf is valid
  IF $adfvalid EQ 1 VAL
    echo "Amiga OS 3.1 Workbench adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
    set adffile "amiga-os-310-workbench.adf"
    SKIP copyadffile
  ENDIF 
ENDIF 

; copy amiga os 3.1 storage adf, if it contains text "Storage3.1"
FSearch >T:_diskname "{file}" TXT="Storage3.1"
set diskname `wc -l <T:_diskname`
Delete >NIL: T:_diskname
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "DosDrivers/RAD" <T:_adflist
  IF $RC EQ 0 VAL
    echo "Amiga OS 3.1 Storage adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
    set adffile "amiga-os-310-storage.adf"
    SKIP copyadffile
  ENDIF
ENDIF

; copy amiga os 3.1 Locale adf, if it contains text "Locale"
FSearch >T:_diskname "{file}" TXT="Locale"
set diskname `wc -l <T:_diskname`
Delete >NIL: T:_diskname
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "Countries/great_britain.country" <T:_adflist
  IF $RC EQ 0 VAL
    echo "Amiga OS 3.1 Locale adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
    set adffile "amiga-os-310-locale.adf"
    SKIP copyadffile
  ENDIF
ENDIF

; copy amiga os 3.1 install adf, if it contains text "Install3.1"
FSearch >T:_diskname "{file}" TXT="Install3.1"
set diskname `wc -l <T:_diskname`
Delete >NIL: T:_diskname
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "HDTools/BRU" <T:_adflist
  IF $RC EQ 0 VAL
    echo "Amiga OS 3.1 Install adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
    set adffile "amiga-os-310-install.adf"
    SKIP copyadffile
  ENDIF
ENDIF

; copy amiga os 3.1 fonts adf, if it contains text "Fonts"
FSearch >T:_diskname "{file}" TXT="Fonts"
set diskname `wc -l <T:_diskname`
Delete >NIL: T:_diskname
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "topaz/11" <T:_adflist
  IF $RC EQ 0 VAL
    echo "Amiga OS 3.1 Fonts adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
    set adffile "amiga-os-310-fonts.adf"
    SKIP copyadffile
  ENDIF
ENDIF

; copy amiga os 3.1 extras adf, if it contains text "Extras3.1"
FSearch >T:_diskname "{file}" TXT="Extras3.1"
set diskname `wc -l <T:_diskname`
Delete >NIL: T:_diskname
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "System/Intellifont" <T:_adflist
  IF $RC EQ 0 VAL
    echo "Amiga OS 3.1 Extras adf" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
    set adffile "amiga-os-310-extras.adf"
    SKIP copyadffile
  ENDIF
ENDIF

SKIP end


; copy adf file
; -------------
LAB copyadffile

echo "Copy '{file}'" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
echo "To '{dir}/$adffile'" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
echo "" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log

; escape filename characters and copy adf file
echo "{file}" NOLINE >T:_escapedadffile
rep T:_escapedadffile "(" "'("
rep T:_escapedadffile ")" "')"
rep T:_escapedadffile "[" "'["
rep T:_escapedadffile "]" "']"
Copy >NIL: "`type T:_escapedadffile`" TO "{dir}/$adffile"
Delete >NIL: T:_escapedadffile


; end
; ---
LAB end
