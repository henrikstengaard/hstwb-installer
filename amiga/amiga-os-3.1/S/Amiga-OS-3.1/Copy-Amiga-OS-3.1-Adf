.KEY file/a,dir/a
.BRA {
.KET }

; Copy Amiga OS 3.1 Adf
; ---------------------
;
; Date: 2018-12-29
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to identify and copy valid Amiga OS 3.1 adf files.


; goto end, if file doesn't exist
IF NOT EXISTS "{file}"
  SKIP end
ENDIF

; add file to log
echo "File = '{file}'" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log

; goto end, if dir doesn't exist
IF NOT EXISTS "{dir}"
  SKIP end
ENDIF

; escape file
echo "{file}" >T:_escapedfile
Execute INSTALLDIR:S/Escape-Filename T:_escapedfile
set escapedfile "`type T:_escapedfile`"
Delete >NIL: T:_escapedfile

; get md5 from file
; uses tempadffile set to unescaped or escaped file depending on md5 error codes
md5 "{file}" >T:_md5
IF $RC EQ 0 VAL
  set tempadffile "{file}"
ELSE
  Copy >NIL: "$escapedfile" TO "INSTALLDIR:Temp/Amiga-OS-310.adf"
  set tempadffile "INSTALLDIR:Temp/Amiga-OS-310.adf"

  md5 "$tempadffile" >T:_md5
  IF NOT $RC EQ 0 VAL
    SKIP end
  ENDIF
ENDIF

; get md5 for file
set md5 ""
set md5 "`type T:_md5`"
Delete >NIL: T:_md5

; add md5 to log
echo "MD5 = '$md5'" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log


; md5
; ---

; copy cloanto amiga forever 2016 amiga os 3.1 workbench adf, if file md5 matches
IF "$md5" eq "590c42a69675d6970df350e200fe25dc"
  echo "Cloanto Amiga Forever 2016 Amiga OS 3.1 Workbench adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-workbench.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7 amiga os 3.1 workbench adf, if file md5 matches
IF "$md5" eq "e7b3a83df665a85e7ec27306a152b171"
  echo "Cloanto Amiga Forever 7 Amiga OS 3.1 Workbench adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-workbench.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 storage adf, if file md5 matches
IF "$md5" eq "4fa1401aeb814d3ed138f93c54a5caef"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Storage adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-storage.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 Locale adf, if file md5 matches
IF "$md5" eq "b53c9ff336e168643b10c4a9cfff4276"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Locale adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-locale.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 2016 amiga os 3.1 install adf, if file md5 matches
IF "$md5" eq "d6aa4537586bf3f2687f30f8d3099c99"
  echo "Cloanto Amiga Forever 2016 Amiga OS 3.1 Install adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-install.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7 amiga os 3.1 install adf, if file md5 matches
IF "$md5" eq "c5be06daf40d4c3ace4eac874d9b48b1"
  echo "Cloanto Amiga Forever 7 Amiga OS 3.1 Install adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-install.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 fonts adf, if file md5 matches
IF "$md5" eq "6fae8b94bde75497021a044bdbf51abc"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Fonts adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-fonts.adf"
  SKIP copyadffile
ENDIF

; copy cloanto amiga forever 7/2016 amiga os 3.1 extras adf, if file md5 matches
IF "$md5" eq "c1c673eba985e9ab0888c5762cfa3d8f"
  echo "Cloanto Amiga Forever 7/2016 Amiga OS 3.1 Extras adf detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-extras.adf"
  SKIP copyadffile
ENDIF

; goto end, if md5 only prefs exists
IF EXISTS INSTALLDIR:Prefs/MD5-Only
  SKIP end
ENDIF


; mount file
; ----------

; goto end, if file size is not 901120 bytes
IF NOT "`wc -c <"$tempadffile"`" EQ 901120 VAL
  SKIP end
ENDIF

; add mount file to log
echo "Mount file" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log

; run mount adf
execute "INSTALLDIR:S/Mount-Adf" "$tempadffile"

; goto end, if adf is not mounted
IF NOT "$adfmounted" EQ 1 VAL
  ; run unmount adf
  execute "INSTALLDIR:S/Unmount-Adf"

  SKIP end
ENDIF

; copy amiga os 3.1 workbench file, if workbench adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1/Detect-Workbench"`" EQ ""
  echo "Amiga OS 3.1 Workbench adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-workbench.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.1 storage file, if storage adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1/Detect-Storage"`" EQ ""
  echo "Amiga OS 3.1 Storage adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-storage.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.1 locale file, if locale adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1/Detect-Locale"`" EQ ""
  echo "Amiga OS 3.1 Locale adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-locale.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.1 install file, if install adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1/Detect-Install"`" EQ ""
  echo "Amiga OS 3.1 Install adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-install.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.1 fonts file, if fonts adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1/Detect-Fonts"`" EQ ""
  echo "Amiga OS 3.1 Fonts adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-fonts.adf"
  SKIP copyadffile
ENDIF

; copy amiga os 3.1 extras file, if extras adf is valid
IF "`Execute "INSTALLDIR:S/Amiga-OS-3.1/Detect-Extras"`" EQ ""
  echo "Amiga OS 3.1 Extras adf detected using mount file" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log
  set adffilename "amiga-os-310-extras.adf"
  SKIP copyadffile
ENDIF

SKIP end


; copy adf file
; -------------
LAB copyadffile

; add copy to log
echo "Copying to '{dir}/$adffilename'" >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log

; make adf file deletable, if it exists
IF EXISTS "{dir}/$adffilename"
  Protect >NIL: "{dir}/$adffilename" +prwed
ENDIF

; copy adf file
Copy >>INSTALLDIR:Temp/Amiga-OS-310-Adf.log "$escapedfile" TO "{dir}/$adffilename"


; end
; ---
LAB end

; run unmount adf
execute "INSTALLDIR:S/Unmount-Adf"
