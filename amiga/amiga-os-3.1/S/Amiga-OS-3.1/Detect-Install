; Detect Amiga OS 3.1 Install
; ---------------------------
;
; Date: 2018-12-07
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to detect valid amiga os 3.1 install floppy disk or adf file.


set diskvalid "0"

; end, if amiga os disk volume name is not "Install3.1:"
IF NOT "`Which $amigaosdisk:`" EQ "Install3.1:"
  SKIP end
ENDIF

; end, if hdtoolbox doesn't exist
IF NOT EXISTS "$amigaosdisk:HDTools/HDToolBox"
  SKIP end
ENDIF

; end, if hdtoolbox is greater than v40.4
Version >NIL: "$amigaosdisk:HDTools/HDToolBox" 40 5 FILE
IF $RC EQ 0 VAL
  SKIP end
ENDIF

; end, if hdtoolbox is less than v40.4
Version >NIL: "$amigaosdisk:HDTools/HDToolBox" 40 3 FILE
IF NOT $RC EQ 0 VAL
  SKIP end
ENDIF

; end, if fastfilesystem doesn't exist
IF NOT EXISTS "$amigaosdisk:L/FastFileSystem"
  SKIP end
ENDIF

; end, if fastfilesystem is greater than v40.1
Version >NIL: "$amigaosdisk:L/FastFileSystem" 40 2 FILE
IF $RC EQ 0 VAL
  SKIP end
ENDIF

; end, if fastfilesystem is less than v40.1
Version >NIL: "$amigaosdisk:L/FastFileSystem" 40 0 FILE
IF NOT $RC EQ 0 VAL
  SKIP end
ENDIF

set diskvalid "1"


; end
; ---

LAB end
