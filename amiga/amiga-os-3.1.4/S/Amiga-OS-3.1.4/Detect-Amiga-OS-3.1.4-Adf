; Detect Amiga OS 3.1.4 Adf
; -------------------------
;
; Date: 2018-10-12
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to detect if Amiga OS 3.1.4 adf files are present.


; detect amiga os 3.1.4 adf files
echo ""
echo "*e[1mDetecting Amiga OS 3.1.4 adf files in AMIGAOSDIR:...*e[0m"

; create amiga os 3.1.4 install directory, if it doesn't exist
IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/"
  MakePath >NIL: "SYSTEMDIR:Temp/Amiga-OS-314"
ENDIF

; delete install amiga os 3.1.4 adf prefs, if it exists
IF EXISTS "INSTALLDIR:Prefs/Install-Amiga-OS-3.1.4-Adf"
  Delete >NIL: "INSTALLDIR:Prefs/Install-Amiga-OS-3.1.4-Adf"
ENDIF

; run detect amiga os 3.1.4 adf, if AMIGAOSDIR: exists and check if all Amiga OS 3.1.4 adf files are present
Assign >NIL: EXISTS AMIGAOSDIR:
IF NOT WARN
  List AMIGAOSDIR: PAT=#? ALL FILES LFORMAT "Execute INSTALLDIR:S/Amiga-OS-3.1.4/Copy-Amiga-OS-3.1.4-Adf *"%p%n*" *"SYSTEMDIR:Temp/Amiga-OS-314*"" >T:_copy_amiga-os-3.1.4_adfs
  Execute T:_copy_amiga-os-3.1.4_adfs
  Delete >NIL: T:_copy_amiga-os-3.1.4_adfs

  ; Set install from adf to 0, if one or more Amiga OS 3.1.4 adf files doesn't exist
  set installfromadf "1"
  IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/amiga-os-314-workbench.adf"
    echo "Amiga OS 3.1.4 Workbench adf doesn't exist!"
    set installfromadf "0"
  ENDIF
  IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/amiga-os-314-locale.adf"
    echo "Amiga OS 3.1.4 Locale adf doesn't exist!"
    set installfromadf "0"
  ENDIF
  IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/amiga-os-314-extras.adf"
    echo "Amiga OS 3.1.4 Extras adf doesn't exist!"
    set installfromadf "0"
  ENDIF
  IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/amiga-os-314-fonts.adf"
    echo "Amiga OS 3.1.4 Fonts adf doesn't exist!"
    set installfromadf "0"
  ENDIF
  IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/amiga-os-314-storage.adf"
    echo "Amiga OS 3.1.4 Storage adf doesn't exist!"
    set installfromadf "0"
  ENDIF
  IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/amiga-os-314-modules-a1200.adf"
    echo "Amiga OS 3.1.4 Modules adf doesn't exist!"
    set installfromadf "0"
  ENDIF
  IF NOT EXISTS "SYSTEMDIR:Temp/Amiga-OS-314/amiga-os-314-install.adf"
    echo "Amiga OS 3.1.4 Install adf doesn't exist!"
    set installfromadf "0"
  ENDIF

  ; create install amiga os 3.1.4 adf prefs, if install from adf is 1
  IF "$installfromadf" EQ 1 VAL
    echo "" >INSTALLDIR:Prefs/Install-Amiga-OS-3.1.4-Adf
  ENDIF
ENDIF


; End
; ---
LAB end

echo "Done"