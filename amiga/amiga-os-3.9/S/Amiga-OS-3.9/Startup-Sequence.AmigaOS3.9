; Startup Sequence for Amiga OS 3.9 Installation
; ----------------------------------------------
;
; Date: 2018-05-23
; Author: Henrik Noerfjand Stengaard


FailAt 21

; Load patched scsi.device v43.45, if it exists
IF EXISTS DEVS:scsi.device
  ; Load scsi.device, if current loaded scsi.device version is less than v43.45
  Version >NIL: "scsi.device" 43 45
  IF WARN
    LoadModule DEVS:scsi.device
  ENDIF
ENDIF

; Run setpatch quiet, if setpatch exists. Use noromupdate, if version is equal or greater than v44.13
IF EXISTS C:SetPatch
  Version >NIL: C:SetPatch 44 13 FILE
  IF WARN
    C:SetPatch QUIET
  ELSE
    C:SetPatch NOROMUPDATE QUIET
  ENDIF
ENDIF


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15


C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET

; Load commands resident 
Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER


; Add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 


; Timezone for unzip
SetEnv TZ MST7

; Data types required for multiview and update screenmode
C:AddDataTypes REFRESH QUIET


; Paths
Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities


; Startup clear screen
cls


; Fail, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "Error: assign hstwb installer doesn't exist!"
  SKIP fail
ENDIF


; Run Assign-HstWB-Installer
execute S:Assign-HstWB-Installer

; Add assigns to installdir
Assign >NIL: C: INSTALLDIR:C ADD
Assign >NIL: Devs: INSTALLDIR:Devs ADD
Assign >NIL: L: INSTALLDIR:L ADD
Assign >NIL: Libs: INSTALLDIR:Libs ADD
Assign >NIL: Prefs: INSTALLDIR:Prefs ADD

; Load commands resident 
Resident >NIL: "INSTALLDIR:C/MakePath" PURE 
Resident >NIL: "INSTALLDIR:C/Reboot" PURE 
Resident >NIL: "INSTALLDIR:C/TotalReset" PURE
Resident >NIL: "INSTALLDIR:C/UAEquit" PURE
Resident >NIL: "INSTALLDIR:C/LhA" PURE
Resident >NIL: "INSTALLDIR:C/UNLZX" PURE
Resident >NIL: "INSTALLDIR:C/UnZip" PURE


; Run install Amiga OS 3.9
cls
execute "INSTALLDIR:S/Install-AmigaOS3.9"


; Replace startup-sequence with workbench startup-sequence
IF EXISTS S:Startup-Sequence.Workbench
  Delete >NIL: S:Startup-Sequence
  Rename >NIL: S:Startup-Sequence.Workbench S:Startup-Sequence
ENDIF


; Remove assigns from installdir
Assign >NIL: C: INSTALLDIR:C REMOVE
Assign >NIL: Devs: INSTALLDIR:Devs REMOVE
Assign >NIL: L: INSTALLDIR:L REMOVE
Assign >NIL: Libs: INSTALLDIR:Libs REMOVE
Assign >NIL: Prefs: INSTALLDIR:Prefs REMOVE


; Set use to 1, if use prefs exists
set uae "0"
IF EXISTS INSTALLDIR:Prefs/UAE
  set uae "1"
ENDIF


; Amiga OS 3.9 installation complete
cls
echo "*e[32m" NOLINE
echo "Amiga OS 3.9 Installation Complete"
echo "*e[0m*e[1m" NOLINE
echo "----------------------------------"
echo "*e[0m" NOLINE
echo ""


IF EXISTS INSTALLDIR:Prefs/Install-BoingBags
  echo "HstWB Installer has completed Amiga OS 3.9 installation and"
  echo "will reboot system to start installation of"
  echo "Boing Bags in Amiga OS 3.9 during WBStartup."
ELSE
  IF EXISTS INSTALLDIR:Prefs/Install-Packages
    ; Copy assign hstwb installer for installing boing bags
    Copy >NIL: INSTALLDIR:S/Assign-HstWB-Installer SYSTEMDIR:S

    ; Replace workbench startup-sequence with packages startup-sequence
    Rename >NIL: SYSTEMDIR:S/Startup-Sequence SYSTEMDIR:S/Startup-Sequence.Workbench
    Copy >NIL: INSTALLDIR:S/Startup-Sequence.Packages SYSTEMDIR:S/Startup-Sequence

    echo "HstWB Installer has completed Amiga OS 3.9 installation and"
    echo "will reboot system to start package installation."
  ELSE
    echo "HstWB Installer has completed Amiga OS 3.9 installation and"
    echo "system is now ready for use."
  ENDIF
ENDIF
echo ""


; Copy hstwb installer log to install dir, if it exists
IF EXISTS SYS:hstwb-installer.log
  Copy >NIL: SYS:hstwb-installer.log INSTALLDIR:hstwb-installer.log
ENDIF

echo "Please wait 10 seconds before restarting to allow file"
echo "system to write changes to disk."
echo ""
echo "Continue will reboot system in 10 seconds."
echo ""
ask "Press ENTER to continue"


; End
; ---
LAB end

echo ""
echo "System will reboot in 10 seconds..."
wait 10
TotalReset >NIL: