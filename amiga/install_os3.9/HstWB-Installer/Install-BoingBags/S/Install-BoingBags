; Install Boing Bags Script
; -------------------------
;
; Date: 2017-07-07
; Author: Henrik Noerfjand Stengaard 
;
; An AmigaDOS script to automate installation of Boing Bags.


; Clear screen
echo "*ec" 


echo "Amiga OS 3.9 Boing Bags installation"
echo "------------------------------------"


; Goto fail, if version.library is not version 45 (Amiga OS 3.9)
Version >NIL: "SYS:Libs/version.library" 45 FILE
IF WARN
  echo ""
  echo "*e[1mERROR: Boing Bags installation requires AmigaOS 3.9!*e[22m"
  SKIP fail
ENDIF


; Goto fail, if device OS39: doesn't exist
Assign >NIL: EXISTS OS39:
IF WARN
  echo ""
  echo "*e[1mERROR: Device OS39: doesn't exist!*e[22m"
  SKIP fail
ENDIF


; Goto fail, if device INSTALL: doesn't exist
Assign >NIL: EXISTS INSTALL:
IF WARN
  echo ""
  echo "*e[1mERROR: Device INSTALL: doesn't exist!*e[22m"
  SKIP fail
ENDIF


; Goto fail, if boing bag 1 file doesn't exist
IF NOT EXISTS "OS39:BoingBag39-1.lha"
  echo ""
  echo "*e[1mERROR: Boing bags doesn't exist in device OS39:. Skipping installation!*e[22m"
  SKIP fail
ENDIF


; Mount FCD0: with Amiga OS 3.9 iso
echo ""
echo "Mounting Amiga OS 3.9 iso..."
Mount FCD0: FROM INSTALL:Devs/Mountlist
wait 10
echo "Done"


; Goto fail, if device AmigaOS3.9: doesn't exist
Assign >NIL: EXISTS AmigaOS3.9:
IF WARN
  echo ""
  echo "*e[1mERROR: Device AmigaOS3.9: doesn't exist!*e[22m"
  SKIP fail
ENDIF


; Create temp boing bags directory
makedir >NIL: SYS:T/BoingBags


; Install Boing Bag 1
execute S:Install-BoingBag1


; Goto clean up, if boing bag 2 file doesn't exist
IF NOT EXISTS "OS39:BoingBag39-2.lha"
  SKIP complete
ENDIF


; Install Boing Bag 2
; Install Boing Bag 3
; Install Boing Bag 4


; Complete
; --------
LAB complete

echo ""
echo "Boing Bags installation is complete"
echo "-----------------------------------"
echo ""
echo "HstWB Installer has completed installation of"
echo "Amiga OS 3.9 Boing Bags and system is now ready for use."


; Clean up
; --------
LAB cleanup

; Delete temp boing bags directory
Delete >NIL: SYS:T/BoingBags ALL

; Delete install boing bags scripts from S:
IF EXISTS S:Install-BoingBags
	Delete >NIL: S:Install-BoingBag#?
ENDIF

; Delete install boing bags scripts from S:
IF EXISTS S:Install-BoingBags
	Delete >NIL: S:Install-BoingBag#?
ENDIF

; Delete start install boing bags from WBStartup, if it exists
IF EXISTS SYS:WBStartup/Start-Install-BoingBags
	Delete >NIL: SYS:WBStartup/Start-Install-BoingBags#?
ENDIF

echo ""
echo "Please let the system wait 10 seconds before continuing"
echo "to allow file system to write changes to disk." 
echo ""

; 
IF EXISTS SYS:Prefs/UAE
  echo "Continue will quit UAE in 10 seconds."
ELSE
  echo "Continue will reboot system in 10 seconds."
ENDIF

SKIP end


; Fail
; ----
LAB fail

; Confirm removing installation of Boing Bags
set confirmremove `RequestChoice "Failed" "Installation of Boing Bags failed and will restart at WBStartup,*Nwhen system is rebooted*N*NDo you want to remove installation of Boing Bags?" "Yes|No"`
IF "$confirmremove" EQ "1"
  SKIP BACK cleanup
ENDIF


; End
; ---
LAB end

echo ""
ask "Press ENTER to continue"
echo ""

; Quit uae, if uae is used. Otherwise reboot
IF EXISTS SYS:Prefs/UAE
  Delete >NIL: SYS:Prefs/UAE
  echo "UAE will quit in 10 seconds..."
  wait 10
  INSTALL:C/UAEquit
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  INSTALL:C/Reboot
ENDIF

ENDSHELL