.KEY file/a,installdir/a
.BRA { 
.KET } 

; Copy Workbench Adf
; ------------------
; Date: 2017-09-03
; Author: Henrik Noerfjand Stengaard
;
; Script to identify Workbench adf files and copy to install dir
; Adf files are identified using MD5 and label name


; Get MD5 for 
set md5 `md5sum2 < "{file}"`


; Copy Cloanto Amiga Forever 2016 Workbench 3.1 Workbench adf, if file MD5 matches
IF $md5 eq "590c42a69675d6970df350e200fe25dc"
  IF NOT EXISTS "{installdir}workbench31workbench.adf"
    echo "Cloanto Amiga Forever Workbench 3.1 Workbench adf..."
    copy >NIL: "{file}" TO "{installdir}workbench31workbench.adf"
    SKIP end
  ENDIF
ENDIF


; Copy Cloanto Amiga Forever 2016 Workbench 3.1 Storage adf, if file MD5 matches
IF $md5 eq "4fa1401aeb814d3ed138f93c54a5caef"
  IF NOT EXISTS "{installdir}workbench31storage.adf"
    echo "Cloanto Amiga Forever Workbench 3.1 Storage adf..."
    copy >NIL: "{file}" TO "{installdir}workbench31storage.adf"
    SKIP end
  ENDIF
ENDIF


; Copy Cloanto Amiga Forever 2016 Workbench 3.1 Locale adf, if file MD5 matches
IF $md5 eq "b53c9ff336e168643b10c4a9cfff4276"
  IF NOT EXISTS "{installdir}workbench31locale.adf"
    echo "Cloanto Amiga Forever Workbench 3.1 Locale adf..."
    copy >NIL: "{file}" TO "{installdir}workbench31locale.adf"
    SKIP end
  ENDIF
ENDIF


; Copy Cloanto Amiga Forever 2016 Workbench 3.1 Install adf, if file MD5 matches
IF $md5 eq "d6aa4537586bf3f2687f30f8d3099c99"
  IF NOT EXISTS "{installdir}workbench31install.adf"
    echo "Cloanto Amiga Forever Workbench 3.1 Install adf..."
    copy >NIL: "{file}" TO "{installdir}workbench31install.adf"
    SKIP end
  ENDIF
ENDIF


; Copy Cloanto Amiga Forever 2016 Workbench 3.1 Fonts adf, if file MD5 matches
IF $md5 eq "6fae8b94bde75497021a044bdbf51abc"
  IF NOT EXISTS "{installdir}workbench31fonts.adf"
    echo "Cloanto Amiga Forever Workbench 3.1 Fonts adf..."
    copy >NIL: "{file}" TO "{installdir}workbench31fonts.adf"
    SKIP end
  ENDIF
ENDIF


; Copy Cloanto Amiga Forever 2016 Workbench 3.1 Extras adf, if file MD5 matches
IF $md5 eq "c1c673eba985e9ab0888c5762cfa3d8f"
  IF NOT EXISTS "{installdir}workbench31extras.adf"
    echo "Cloanto Amiga Forever Workbench 3.1 Extras adf..."
    copy >NIL: "{file}" TO "{installdir}workbench31extras.adf"
    SKIP end
  ENDIF
ENDIF


; Goto end, if file size is not 901120 bytes
set filesize `wc -c <"{file}"`
IF NOT $filesize EQ 901120 VAL
  SKIP end
ENDIF


; Goto end, if xadunfile doesn't return error code 0
xadUnFile >T:_adflist "{file}" DIMG LIST
IF NOT $RC EQ 0
  SKIP end
ENDIF


; Copy Workbench 3.1 Workbench adf, if it contains text "Workbench 3.1"
FSearch >T:_diskname "{file}" TXT="Workbench3.1"
set diskname `wc -l <T:_diskname`
IF NOT $diskname EQ 0 VAL
  set workbenchadfvalid 1

  ; Set workbench adf valid 0, if adf doesn't contain which file
  grep >NIL: -i "C/Which" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set workbenchadfvalid 0
  ENDIF

  ; Set workbench adf valid 0, if adf doesn't contain assign file
  grep >NIL: -i "C/Assign" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set workbenchadfvalid 0
  ENDIF

  ; Set workbench adf valid 0, if adf doesn't contain datatypes.library file
  grep >NIL: -i "Libs/datatypes.library" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set workbenchadfvalid 0
  ENDIF

  ; Set workbench adf valid 0, if adf doesn't contain cli file
  grep >NIL: -i "System/CLI" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set workbenchadfvalid 0
  ENDIF

  ; Set workbench adf valid 0, if adf doesn't contain shell.info file
  grep >NIL: -i "System/Shell.info" <T:_adflist
  IF NOT $RC EQ 0 VAL
    set workbenchadfvalid 0
  ENDIF

  ; Copy workbench 3.1 workbench file, if workbench adf is valid
  IF $workbenchadfvalid EQ 1 VAL
    IF NOT EXISTS "{installdir}workbench31workbench.adf"
      echo "Workbench 3.1 Workbench adf..."
      copy >NIL: "{file}" TO "{installdir}workbench31workbench.adf"
      SKIP end
    ENDIF
  ENDIF 
ENDIF 


; Copy Workbench 3.1 Storage adf, if it contains text "Storage3.1"
FSearch >T:_diskname "{file}" TXT="Storage3.1"
set diskname `wc -l <T:_diskname`
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "DosDrivers/RAD" <T:_adflist
  IF $RC EQ 0 VAL
    IF NOT EXISTS "{installdir}workbench31storage.adf"
      echo "Workbench 3.1 Storage adf..."
      copy >NIL: "{file}" TO "{installdir}workbench31storage.adf"
      SKIP end
    ENDIF
  ENDIF
ENDIF


; Copy Workbench 3.1 Locale adf, if it contains text "Locale"
FSearch >T:_diskname "{file}" TXT="Locale"
set diskname `wc -l <T:_diskname`
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "Countries/great_britain.country" <T:_adflist
  IF $RC EQ 0 VAL
    IF NOT EXISTS "{installdir}workbench31locale.adf"
      echo "Workbench 3.1 Locale adf..."
      copy >NIL: "{file}" TO "{installdir}workbench31locale.adf"
      SKIP end
    ENDIF
  ENDIF
ENDIF


; Copy Workbench 3.1 Install adf, if it contains text "Install3.1"
FSearch >T:_diskname "{file}" TXT="Install3.1"
set diskname `wc -l <T:_diskname`
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "HDTools/BRU" <T:_adflist
  IF $RC EQ 0 VAL
    IF NOT EXISTS "{installdir}workbench31install.adf"
      echo "Workbench 3.1 Install adf..."
      copy >NIL: "{file}" TO "{installdir}workbench31install.adf"
      SKIP end
    ENDIF
  ENDIF
ENDIF


; Copy Workbench 3.1 Fonts adf, if it contains text "Fonts"
FSearch >T:_diskname "{file}" TXT="Fonts"
set diskname `wc -l <T:_diskname`
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "topaz/11" <T:_adflist
  IF $RC EQ 0 VAL
    IF NOT EXISTS "{installdir}workbench31fonts.adf" 
      echo "Workbench 3.1 Fonts adf..."
      copy >NIL: "{file}" TO "{installdir}workbench31fonts.adf"
      SKIP end
    ENDIF
  ENDIF
ENDIF


; Copy Workbench 3.1 Extras adf, if it contains text "Extras3.1"
FSearch >T:_diskname "{file}" TXT="Extras3.1"
set diskname `wc -l <T:_diskname`
IF NOT $diskname EQ 0 VAL
  grep >NIL: -i "System/Intellifont" <T:_adflist
  IF $RC EQ 0 VAL
    IF NOT EXISTS "{installdir}workbench31extras.adf"
      echo "Workbench 3.1 Extras adf..."
      copy >NIL: "{file}" TO "{installdir}workbench31extras.adf"
      SKIP end
    ENDIF
  ENDIF
ENDIF


; End
LAB end