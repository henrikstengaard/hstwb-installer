; Install Boing Bags Script
; -------------------------
;
; Date: 2017-07-11
; Author: Henrik Noerfjand Stengaard 
;
; An AmigaDOS script to automate installation of Boing Bags.


; Clear screen
echo "*ec" 


echo "Amiga OS 3.9 Boing Bags installation"
echo "------------------------------------"


; Goto end, if version.library is not version 45 (Amiga OS 3.9)
Version >NIL: "SYS:Libs/version.library" 45 FILE
IF WARN
  echo ""
  echo "*e[1mERROR: Boing Bags installation requires AmigaOS 3.9!*e[22m"
  SKIP end
ENDIF


; Goto end, if device OS39: doesn't exist
Assign >NIL: EXISTS OS39:
IF WARN
  echo ""
  echo "*e[1mERROR: Device OS39: doesn't exist!*e[22m"
  SKIP end
ENDIF


; Goto end, if device INSTALL: doesn't exist
Assign >NIL: EXISTS INSTALL:
IF WARN
  echo ""
  echo "*e[1mERROR: Device INSTALL: doesn't exist!*e[22m"
  SKIP end
ENDIF


; Goto end, if boing bag 1 file doesn't exist
IF NOT EXISTS "OS39:BoingBag39-1.lha"
  echo ""
  echo "*e[1mERROR: Boing bags doesn't exist in device OS39:. Skipping installation!*e[22m"
  SKIP end
ENDIF


; Goto mount amiga os 3.9 iso, if device CD0: doesn't exists
Assign >NIL: EXISTS CD0:
IF WARN
  SKIP mountamigaos39iso
ENDIF


; Goto check amiga os 3.9 cd, if CD0:OS-VERSION3.9.INFO exists
IF EXISTS CD0:OS-VERSION3.9.INFO
  SKIP checkamigaos39cd
ENDIF


; Mount FCD0: with Amiga OS 3.9 iso
; ---------------------------------
LAB mountamigaos39iso

echo ""
echo "CD Drive device CD0: not found."
echo ""
echo "Installation process will fallback to mounting"
echo "Amiga OS 3.9 iso as a virtual cd-rom drive."
echo ""
ask "Press ENTER to continue"

echo ""
echo "Mounting Amiga OS 3.9 iso..."
Mount FCD0: FROM INSTALL:Devs/Mountlist
wait 5
echo "Done"


; Goto end, if device FCD0: doesn't exist
Assign >NIL: EXISTS FCD0:
IF WARN
  echo ""
  echo "*e[1mERROR: Device FCD0: doesn't exist!*e[22m"
  SKIP end
ENDIF


; Check Amiga OS 3.9 CD
; ---------------------
LAB checkamigaos39cd

; Goto end, if device AmigaOS3.9: doesn't exist
Assign >NIL: EXISTS AmigaOS3.9:
IF WARN
  echo ""
  echo "*e[1mERROR: Device AmigaOS3.9: doesn't exist!*e[22m"
  SKIP end
ENDIF


; Create temp boing bags directory
makedir >NIL: SYS:T/BoingBags


; Install Boing Bag 1
execute S:Install-BoingBag1


; Goto complete, if boing bag 2 file doesn't exist
IF NOT EXISTS "OS39:BoingBag39-2.lha"
  SKIP complete
ENDIF


; Install Boing Bag 2
execute S:Install-BoingBag2


; Complete
; --------
LAB complete

; Delete temp boing bags directory
Delete >NIL: SYS:T/BoingBags ALL

; Delete install boing bags scripts
Delete >NIL: S:Copy-Locale
Delete >NIL: S:Install-BoingBag#?
Delete >NIL: SYS:WBStartup/Start-Install-BoingBags#?

; Replace workbench startup-sequence with packages startup-sequence, if install packages prefs exists. Otherwise delete packages startup sequence
IF EXISTS SYS:Prefs/Install-Packages
  Rename >NIL: S:Startup-Sequence S:Startup-Sequence.Workbench
  Rename >NIL: S:Startup-Sequence.Packages S:Startup-Sequence
ELSE
  Delete >NIL: S:Startup-Sequence.Packages
ENDIF

echo ""
echo "Boing Bags installation is complete."
echo ""

IF EXISTS SYS:Prefs/Install-Packages
  echo "HstWB Installer has completed installation of"
  echo "Amiga OS 3.9 Boing Bags and will now reboot to"
  echo "start package installation."
ELSE
  echo "HstWB Installer has completed installation of"
  echo "Amiga OS 3.9 Boing Bags and system is now"
  echo "ready for use."
ENDIF

echo ""
echo "Please let the system wait 10 seconds before continuing"
echo "to allow file system to write changes to disk." 


; Reboot system, if install packages prefs exist
IF EXISTS SYS:Prefs/Install-Packages
  echo ""
  ask "Press ENTER to continue"
  echo ""
  echo "System will reboot in 10 seconds..."
  wait 10
  INSTALL:C/TotalReset
ENDIF


; End
; ---
LAB end

echo ""
ask "Press ENTER to continue"
echo ""


; Quit uae, if uae is used. Otherwise reboot
IF EXISTS SYS:Prefs/UAE
  Delete >NIL: SYS:Prefs/UAE
  echo "UAE will quit in 10 seconds..."
  wait 10
  INSTALL:C/UAEquit
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  INSTALL:C/Reboot
ENDIF

ENDSHELL