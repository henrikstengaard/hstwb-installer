.KEY userpackagesfile/a
.BRA {
.KET }

; Find User Packages
; ------------------
;
; Date: 2017-09-26
; Author: Henrik Noerfjand Stengaard

echo ""
echo "Finding user packages in USERPACKAGESDIR:"

; Create empty user packages file
echo "" NOLINE >"{userpackagesfile}"

; Goto end, if USERPACKAGESDIR: device name doesn't exist
Assign >NIL: EXISTS USERPACKAGESDIR:
IF WARN
  SKIP end
ENDIF 

; List directories in USERPACKAGESDIR:
Dr "USERPACKAGESDIR:" -DO -[\f] >T:_userpackages1
sed "s/\/$//" T:_userpackages1 >T:_userpackages2
frsort <T:_userpackages2 >T:_userpackages3

; Get next user package from user packages
LAB next
set userpackage ""
set userpackage "`FirstLine T:_userpackages3 DELETE`"

; Goto end, if userpackage is empty
IF "$userpackage" EQ ""
  SKIP end
ENDIF

; Goto next, if user package dir doesn't contain a _installdir assign file
IF NOT EXISTS "USERPACKAGESDIR:$userpackage/_installdir"
  echo "*e[1mWARNING: User package '$userpackage' doesn't contain '_installdir' assign file, skipped!*e[22m"
  SKIP BACK next
ENDIF

; Goto next, if user package dir is empty
Dr "USERPACKAGESDIR:$userpackage" -O -[\f] >T:_userpackagesfiles
Set userpackagesfiles `wc -l <T:_userpackagesfiles`
Delete >NIL: T:_userpackagesfiles
IF $userpackagesfiles EQ 1 VAL
  echo "*e[1mWARNING: User package '$userpackage' is empty, skipped!*e[22m"
  SKIP BACK next
ENDIF

; Print user package and add user package to user packages file
echo "$userpackage"
echo "$userpackage" >>"{userpackagesfile}"

SKIP BACK next


; End
LAB end

echo "Done"

; Delete temp files, if they exist
IF EXISTS T:_userpackages
  delete >NIL: T:_userpackages#?
ENDIF