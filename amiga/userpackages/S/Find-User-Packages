.KEY userpackagesfile/a
.BRA {
.KET }

; Find User Packages
; ------------------
;
; Date: 2018-01-04
; Author: Henrik Noerfjand Stengaard

echo ""
echo "*e[1mFinding user packages in USERPACKAGESDIR:*e[0m"

; Create empty user packages file
echo "" NOLINE >"{userpackagesfile}"

; Goto end, if USERPACKAGESDIR: device name doesn't exist
Assign >NIL: EXISTS USERPACKAGESDIR:
IF WARN
  SKIP end
ENDIF 

; List directories in USERPACKAGESDIR:
Dr "USERPACKAGESDIR:" -DO -[\f] >T:_userpackages1
sed "s/\/$//" T:_userpackages1 >T:_userpackages2
frsort <T:_userpackages2 >T:_userpackages3

; Get next user package from user packages
LAB next
set userpackage ""
set userpackage `Execute INSTALLDIR:S/FirstLine T:_userpackages3`

; Goto end, if userpackage is empty
IF "$userpackage" EQ ""
  SKIP end
ENDIF

; Goto next, if user package dir doesn't contain a _installdir assign file
IF NOT EXISTS "USERPACKAGESDIR:$userpackage/_installdir"
  SKIP BACK next
ENDIF

; Print user package and add user package to user packages file
echo "$userpackage"
echo "$userpackage" >>"{userpackagesfile}"

SKIP BACK next


; End
LAB end

echo "Done"

; Delete temp files, if they exist
IF EXISTS T:_userpackages
  delete >NIL: T:_userpackages#?
ENDIF