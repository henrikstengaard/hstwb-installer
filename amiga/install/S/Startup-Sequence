; Startup Sequence for HstWB Installer
; ------------------------------------
;
; Date: 2017-07-05
; Author: Henrik Noerfjand Stengaard


; Load patched scsi.device v43.45
LoadModule DEVS:scsi.device


; Clear screen
echo "*ec" 


; Install message
echo "Install"
echo "-------"
echo ""
echo "HstWB Installer running install..."


; Set stack, failat and load basic commands resident 
stack 16384
failat 21
DF0:C/AddBuffers >NIL: DF0: 200
resident DF0:C/Assign PURE
resident DF0:C/Copy PURE
resident DF0:C/Delete PURE
resident DF0:C/Execute PURE
resident DF0:C/Rename PURE
resident DF0:C/MakeDir PURE
resident DF0:C/Protect PURE
resident DF0:C/Search PURE 
resident DF0:C/Wait PURE 
resident DF0:C/Which PURE 
resident DF0:C/Mount PURE
resident DF0:C/Type PURE
resident DF0:C/Version PURE
resident DF0:C/RequestChoice PURE
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 
MakeDir RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
Assign T: RAM:
SetEnv TZ MST7


wait 3


; Fail, if user assign doesn't exist
IF NOT EXISTS S:User-Assign
  echo "" >INSTALL:Error
  echo "Error: User assign doesn't exist!"
  SKIP end
ENDIF
execute S:User-Assign


; Install HstWB Installer Packages
IF EXISTS INSTALL:HstWB-Installer.Packages.ini
  makepath >NIL: SYSTEMDIR:Prefs/Env-Archive
  copy >NIL: INSTALL:HstWB-Installer.Packages.ini SYSTEMDIR:Prefs/Env-Archive/HstWB-Installer.Packages.ini
ENDIF


; Install HstWB Installer Assigns
IF EXISTS INSTALL:HstWB-Installer.Assigns.ini
  makepath >NIL: SYSTEMDIR:Prefs/Env-Archive
  copy >NIL: INSTALL:HstWB-Installer.Assigns.ini SYSTEMDIR:Prefs/Env-Archive/HstWB-Installer.Assigns.ini
ENDIF


; Install Workbench, if prefs exists
IF EXISTS SYS:Prefs/Install-Workbench
  execute S:Install-Workbench
ENDIF


; Install Amiga OS 3.9, if prefs exists
IF EXISTS SYS:Prefs/Install-AmigaOS3.9
  execute S:Install-AmigaOS3.9
ENDIF


; Install Kickstart, if prefs exists
IF EXISTS SYS:Prefs/Install-Kickstart
  execute S:Install-Kickstart
ENDIF


; Install Packages, if install boing bags prefs doesn't exist and install packages prefs exists
IF NOT EXISTS SYS:Prefs/Install-BoingBags
  IF EXISTS SYS:Prefs/Install-Packages
    execute S:Install-Packages
  ENDIF
ENDIF


; Delete Installing file, if it exists
IF EXISTS S:Installing
  delete >NIL: S:Installing
ENDIF


; Clear screen
echo "*ec" 


; Install complete message
echo "Install complete"
echo "----------------"
echo ""

IF EXISTS SYS:Prefs/Install-BoingBags
  IF EXISTS SYS:Prefs/UAE
    echo "HstWB Installer has completed install and will quit and"
    echo "restart UAE to start installation of Boing Bags in"
    echo "Amiga OS 3.9 during WBStartup."
  ELSE
    echo "HstWB Installer has completed install and will reboot"
    echo "system to start installation of Boing Bags in"
    echo "Amiga OS 3.9 during WBStartup."
  ENDIF
ELSE
  echo "HstWB Installer has completed install and system is"
  echo "now ready for use"
ENDIF

echo ""
echo "Please let the system wait 10 seconds before continuing"
echo "to allow file system to write changes to disk." 
echo ""

; Quit UAE or reboot message
IF EXISTS SYS:Prefs/UAE
  echo "Continue will quit UAE in 10 seconds."
ELSE
  echo "Continue will reboot system in 10 seconds."
ENDIF

echo ""
ask "Press ENTER to continue"
echo ""


; End
LAB end

; Quit UAE, if UAE is used. Otherwise reboot
IF EXISTS SYS:Prefs/UAE
  Delete >NIL: SYS:Prefs/UAE
  echo "UAE will quit in 10 seconds..."
  wait 10
  INSTALL:C/UAEquit
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  INSTALL:C/Reboot
ENDIF