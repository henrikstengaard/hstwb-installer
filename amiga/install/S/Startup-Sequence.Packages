; HstWB Installer Script
; ----------------------
;
; Date: 2017-07-31
; Author: Henrik Noerfjand Stengaard 
;
; An AmigaDOS script to start installation of packages.


C:SetPatch NOROMUPDATE QUIET


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15
FailAt 21

C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
C:Copy >NIL: ENVARC: RAM:ENV ALL NOREQ

Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE

Assign >NIL: ENV: RAM:ENV
Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER

Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 

; Timezone for unzip
SetEnv TZ MST7


; Data types required for multiview and update screenmode
C:AddDataTypes REFRESH QUIET

; TODO: Causes intuition attempted to reset workbench screen errors, when installing ClassicWB packages
; TODO: Find way of getting max colors for workbench for screenshots in readme
;C:IPrefs

; Paths
Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities


; Clear screen
echo "*ec" 


; Fail, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "Error: assign hstwb installer doesn't exist!"
  SKIP fail
ENDIF


; Run Assign-HstWB-Installer
execute S:Assign-HstWB-Installer


; Install Packages
execute S:Install-Packages


; Replace packages startup-sequence with workbench startup-sequence
Rename >NIL: S:Startup-Sequence S:Startup-Sequence.Packages
Rename >NIL: S:Startup-Sequence.Workbench S:Startup-Sequence
Delete >NIL: S:Startup-Sequence.Packages


; Clean up
; --------

;Delete >NIL: S:HstWB-Installer

; Delete start hstwb installer from WBStartup, if it exists
;IF EXISTS SYS:Start-HstWB-Installer
;	Delete >NIL: SYS:Start-HstWB-Installer#?
;ENDIF


; Clear screen
echo "*ec" 


; Package installation complete
echo "Package Installation Complete"
echo "-----------------------------"
echo ""
echo "HstWB Installer has completed package"
echo "installation and system is now ready for use."
echo ""
echo "Please wait 10 seconds before restarting to allow file"
echo "system to write changes to disk. Continue will reboot"
echo "the system in 10 seconds."
echo ""

; Quit UAE or reboot message
IF EXISTS SYS:Prefs/UAE
  echo "Continue will quit UAE in 10 seconds."
ELSE
  echo "Continue will reboot system in 10 seconds."
ENDIF

echo ""
ask "Press ENTER to continue"


; End
; ---
LAB end

echo ""

; Quit UAE, if UAE is used. Otherwise reboot
IF EXISTS SYS:Prefs/UAE
  Delete >NIL: SYS:Prefs/UAE
  echo "UAE will quit in 10 seconds..."
  wait 10
  INSTALLDIR:C/UAEquit
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  INSTALLDIR:C/Reboot
ENDIF