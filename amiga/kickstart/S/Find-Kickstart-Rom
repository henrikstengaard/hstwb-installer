; Find Kickstart Rom
; ------------------
;
; Date: 2018-05-02
; Author: Henrik Noerfjand Stengaard

; Find Kickstart rom files
echo ""
echo "*e[1mFinding Kickstart rom files in KICKSTARTDIR:...*e[0m"

; Search for kickstart rom and key files in KICKSTARTDIR:
fsearch KICKSTARTDIR:#?.rom >T:_kickstartromfiles
fsearch KICKSTARTDIR:#?.key >>T:_kickstartromfiles

; Get next rom file
LAB nextromfile
set romfile ""
set romfile `Execute INSTALLDIR:S/FirstLine "T:_kickstartromfiles"`

; Goto end, if romfile is empty
IF "$romfile" EQ ""
  SKIP end
ENDIF

; Get MD5 for romfile
set md5 "`md5 "$romfile"`"

; Copy Cloanto Amiga Forever Kickstart 1.2 (33.180) (A500) rom, if file MD5 matches
IF $md5 eq "c56ca2a3c644d53e780a7e4dbdc6b699"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick33180.A500"
    echo "Cloanto Amiga Forever Kickstart 1.2 (33.180) (A500) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick33180.A500"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Cloanto Amiga Forever Kickstart 1.3 (34.5) (A500) rom, if file MD5 matches
IF $md5 eq "89160c06ef4f17094382fc09841557a6"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick34005.A500"
    echo "Cloanto Amiga Forever Kickstart 1.3 (34.5) (A500) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick34005.A500"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Cloanto Amiga Forever Kickstart 3.1 (40.063) (A600) rom, if file MD5 matches
IF $md5 eq "c3e114cd3b513dc0377a4f5d149e2dd9"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick40063.A600"
    echo "Cloanto Amiga Forever Kickstart 3.1 (40.063) (A600) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick40063.A600"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Cloanto Amiga Forever Kickstart 3.1 (40.068) (A1200) rom, if file MD5 matches
IF $md5 eq "dc3f5e4698936da34186d596c53681ab"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick40068.A1200"
    echo "Cloanto Amiga Forever Kickstart 3.1 (40.068) (A1200) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick40068.A1200"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Cloanto Amiga Forever Kickstart 3.1 (40.068) (A4000) rom, if file MD5 matches
IF $md5 eq "8b54c2c5786e9d856ce820476505367d"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick40068.A4000"
    echo "Cloanto Amiga Forever Kickstart 3.1 (40.068) (A4000) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick40068.A4000"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Cloanto Amiga Forever Kickstart rom key, if it exists
echo "{file}" NOLINE >T:_kickstartromfile
IF "`wc -l <T:_kickstartromfile`" EQ 7 VAL
  search >NIL: T:_kickstartromfile "rom.key"
  IF $RC EQ 0 VAL
    IF NOT EXISTS "INSTALLDIR:Kickstart/rom.key"
      echo "Cloanto Amiga Forever Kickstart rom key"
      set kickstartromfile "INSTALLDIR:Kickstart/rom.key"
      SKIP copyromfile
    ENDIF
  ENDIF
ENDIF

; Copy Kickstart 1.2 (33.180) (A500) rom, if file MD5 matches
IF $md5 eq "85ad74194e87c08904327de1a9443b7a"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick33180.A500"
    echo "Kickstart 1.2 (33.180) (A500) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick33180.A500"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Kickstart 1.3 (34.5) (A500) rom, if file MD5 matches
IF $md5 eq "82a21c1890cae844b3df741f2762d48d"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick34005.A500"
    echo "Kickstart 1.3 (34.5) (A500) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick34005.A500"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Kickstart 3.1 (40.063) (A600) rom, if file MD5 matches
IF $md5 eq "e40a5dfb3d017ba8779faba30cbd1c8e"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick40063.A600"
  echo "Kickstart 3.1 (40.063) (A600) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick40063.A600"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Kickstart 3.1 (40.068) (A1200) rom, if file MD5 matches
IF $md5 eq "646773759326fbac3b2311fd8c8793ee"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick40068.A1200"
    echo "Kickstart 3.1 (40.068) (A1200) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick40068.A1200"
    SKIP copyromfile
  ENDIF
ENDIF

; Copy Kickstart 3.1 (40.068) (A4000) rom, if file MD5 matches
IF $md5 eq "9bdedde6a4f33555b4a270c8ca53297d"
  IF NOT EXISTS "INSTALLDIR:Kickstart/kick40068.A4000"
    echo "Kickstart 3.1 (40.068) (A4000) rom"
    set kickstartromfile "INSTALLDIR:Kickstart/kick40068.A4000"
    SKIP copyromfile
  ENDIF
ENDIF

SKIP BACK nextromfile


; Copy rom file
; -------------
LAB copyromfile

; Escase characters ( ) [ ] ~ | # ? % `
; http://www.amiga.org/forums/archive/index.php/t-23790.html
echo "$romfile" NOLINE >T:_kickstartromfile
rep T:_kickstartromfile "(" "'("
rep T:_kickstartromfile ")" "')"
rep T:_kickstartromfile "[" "'["
rep T:_kickstartromfile "]" "']"
copy >NIL: "`type T:_kickstartromfile`" TO "$kickstartromfile"

SKIP BACK nextromfile


; End
; ---
LAB end

echo "Done"

IF EXISTS T:_kickstartromfiles
  Delete >NIL: T:_kickstartromfiles
ENDIF
IF EXISTS T:_kickstartromfile
  Delete >NIL: T:_kickstartromfile
ENDIF